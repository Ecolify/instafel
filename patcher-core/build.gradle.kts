import groovy.util.Node
import org.gradle.kotlin.dsl.from

plugins {
    kotlin("jvm") version "2.2.0"
    java
    `java-library`
    `maven-publish`
}

val libs = rootProject.extra["patcherLibs"] as Map<*, *>
val config = rootProject.extra["instafelConfig"] as Map<*, *>
val projectConfig = config["patcher"] as Map<*, *>
val coreSupportedVersion = projectConfig["core_supported_version"] as String
val commitHash: String by rootProject.extra

group = "patcher"
version = commitHash

repositories {
    mavenCentral()
    google()
    maven("https://jitpack.io")
}

dependencies {
    implementation(kotlin("stdlib"))
    implementation(libs["kotlin-reflect"]!!)
    implementation(libs["org-json"]!!)
    implementation(libs["commons-io"]!!)
    implementation(libs["okhttp"]!!)
    implementation(libs["apktool-lib"]!!)
    implementation(libs["classgraph"]!!)
    implementation(libs["jackson-databind"]!!)
    implementation(libs["jackson-yaml"]!!)
}

tasks.named<Jar>("jar") {
    archiveBaseName.set("ifl-patcher-core")
    archiveClassifier.set("")
    destinationDirectory.set(file("${rootProject.rootDir}/.output"))

    manifest {
        attributes(
            "Patcher-Core-Commit" to commitHash,
            "Patcher-Core-Supported-Version" to coreSupportedVersion,
            "Patcher-Core-Branch" to "main"
        )
    }

    from("$buildDir/generated-poms/pom.xml") {
        into("META-INF/maven/instafel/patcher/core")
        rename { "pom.xml" }
    }
}

tasks.jar {
    dependsOn("generatePomFile")
}

tasks.register("build-jar") {
    dependsOn("jar")

    doLast {
        println("JAR successfully generated.")
    }
}

publishing {
    publications {
        create<MavenPublication>("publish-patcher") {
            groupId = "instafel"
            artifactId = "patcher.core"
            version = commitHash

            // JAR artifact
            artifact(tasks.jar.get().archiveFile) {
                builtBy(tasks.jar)
            }

            // POM artifact (generated by other task instead of Maven Publication)
            pom {
                tasks.named("publish") {
                    dependsOn("generatePomFile")
                }

                val pomFile = file("$buildDir/generated-poms/pom.xml")

                withXml {
                    val parser = groovy.xml.XmlParser()
                    val pomNode = parser.parse(pomFile)

                    asNode().children().clear()

                    pomNode.children().forEach { child ->
                        asNode().append(child as Node?)
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/mamiiblt/instafel")
            credentials {
                username = findProperty("gpr.user") as String? ?: System.getenv("GITHUB_USER")
                password = findProperty("gpr.key") as String? ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

tasks.register("generatePomFile") {
    group = "build"
    description = "Generates POM file for Instafel Patcher dynamically"

    doLast {
        val jarFile = tasks.jar.get().archiveFile.get().asFile
        val pomFile = File("$buildDir/generated-poms/pom.xml")
        pomFile.parentFile.mkdirs()

        pomFile.writeText(
            """<?xml version="1.0" encoding="UTF-8"?>
<project xmlns="http://maven.apache.org/POM/4.0.0" xsi:schemaLocation="http://maven.apache.org/POM/4.0.0 https://maven.apache.org/xsd/maven-4.0.0.xsd" xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance">
  <modelVersion>4.0.0</modelVersion>
  <groupId>instafel</groupId>
  <artifactId>patcher.core</artifactId>
  <version>$commitHash</version>
  <name>Instafel Patcher Core</name>
  <description># Instafel Patcher Core

Instafel Patcher Core is a JAR file containing core patcher components such as patch utilities. A regular user does not need to manually download this JAR file.

## Build Information

| Property | Value |
| ------------- | ------------- |
| Supported Patcher  | v$coreSupportedVersion |
| Commit  | [$commitHash](https://github.com/mamiiblt/instafel/commit/$commitHash)  |
| Branch  | [main](https://github.com/mamiiblt/instafel) |

## More Information?

For more information about the patcher, please visit [Instafel Wiki](instafel.app/wiki)</description>
  <url>https://github.com/mamiiblt/instafel</url>
</project>
"""
        )
        println("POM file generated succesfully")
    }
}
